#
# A single line theme with Git information on the rigth
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Yan Pritzker <yan@pritzker.ws>
#   Rufus Deponian <mail@site>
#

function prompt_user_precmd {
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_user_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_user_precmd

  # Tell prezto we can manage this prompt
  zstyle ':prezto:module:prompt' managed 'yes'


  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    colors=(
      "%F{202}"	# 1 Hostname
      "%F{196}"	# 2 Login
      "%F{226}"	# 3 Current directory
	  "%F{7}"	# 4 Delimeter between hostname and login
	  "%F{241}"	# 5 Git. Branch name
	  "%F{10}"	# 6 Git. Added files
	  "%F{202}"	# 7 Git. Deletes files
	  "%F{220}"	# 8 Git. Modified files
	  "%F{124}"	# 9 Git. Untracked files
    )
  else
    colors=(
      "%F{yellow}"	# 1 Hostname	
      "%F{red}"		# 2 Login
      "%F{white}"	# 3 Current directory
      "%F{white}"	# 4 Delimeter between hostname and login
	  "%F{cyan}"	# 5 Git. Branch name
	  "%F{green}"	# 6 Git. Added files
	  "%F{red}"		# 7 Git. Deletes files
	  "%F{yellow}"	# 8 Git. Modified files
	  "%F{red}"		# 9 Git. Untracked files
    )
  fi

  # Set dot symbol for git status in case non UTF-8 terminal
  if [[ "$TERM" == (dumb|linux|*bsd*) ]]; then
	  DOT='*'
  else
	  DOT='‚óè'
  fi

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format "${colors[5]}%b%f"
  zstyle ':prezto:module:git:info:added' format "${colors[6]}$DOT%f "
  zstyle ':prezto:module:git:info:deleted' format "${colors[7]}$DOT%f "
  zstyle ':prezto:module:git:info:modified' format "${colors[8]}$DOT%f "
  zstyle ':prezto:module:git:info:untracked' format "${colors[9]}$DOT%f "
  zstyle ':prezto:module:git:info:keys' format 'prompt' '%d%u%m%a%b'

  # Define prompts.
  PROMPT='${colors[2]}%n%f${colors[4]}@%f${colors[1]}%m%f ${colors[3]}%~%f '
  RPROMPT='${git_info:+${(e)git_info[prompt]}}'
}

prompt_user_setup "$@"
# vim: ft=zsh
